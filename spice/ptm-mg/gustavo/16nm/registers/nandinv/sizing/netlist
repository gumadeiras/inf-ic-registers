simulator lang=spice

.lib '../../../../../modelfiles/models' ptm16lstp
.lib '../../../../../library' standardCells

.option post=2
.global gnd! vdd!
.param v_supply = 0.85


****************************************************
************ ** INVERTER
****************************************************
.SUBCKT sinv vi vo cvdd cgnd
Mn0 vo vi cgnd cgnd nfet nfin="Wn"
Mp0 vo vi cvdd cvdd pfet nfin="2*Wn"
.ENDS 

****************************************************
************** NAND2
****************************************************
.SUBCKT snand2 in0 in1 out cvdd cgnd
Mn0 out in0 nnode cgnd nfet nfin="Wn"
Mn1 nnode in1 cgnd cgnd nfet nfin="Wn"
Mp0 out in0 cvdd cvdd pfet nfin="2*Wn"
Mp1 out in1 cvdd cvdd pfet nfin="2*Wn"
.ENDS

****************************************************
************** REGISTER - NAND INV DFF
****************************************************
.SUBCKT snandinv data out clk cvdd cgnd
XNAND1 out4 out2 out1 cvdd cgnd snand2
XNAND2 out1 clk  out2 cvdd cgnd snand2
XNAND3 out2 out4 out3 cvdd cgnd snand2
XNAND4 out6 data out4 cvdd cgnd snand2
XINV5  out3 out5 cvdd cgnd sinv
XNAND6 clk  out5 out6 cvdd cgnd snand2
XNAND7 out2 out8 out cvdd cgnd  snand2
XNAND8 out  out6 out8 cvdd cgnd snand2
.ENDS

**********DEFINE CIRCUIT************
*-------SETUP----------
xinv2S CLKS0 CLKS1 vdd! gnd! sinv
xinv3S CLKS1 CLKS vdd! gnd! sinv

xinvS0 DS0 DS1 vdd! gnd! sinv
xinvS1 DS1 DS vdd! gnd! sinv

xRegS DS QS CLKS X0 gnd! snandinv

xinvQS QS notQS vdd! gnd! sinv
cS1 notQS 0 5f
xInvQS2 QS notQS2 vdd! gnd! sinv
cS2 notQS2 gnd! 5f
xInvQS3 QS notQS3 vdd! gnd! sinv
cS3 notQS3 gnd! 5f
xinvQS4 QS notQS4 vdd! gnd! sinv
cS4 notQS4 gnd! 5f

*-------HOLD---------------
xinv2H CLKH0 CLKH1 vdd! gnd! sinv
xinv3H CLKH1 CLKH vdd! gnd! sinv

xinvH0 DH0 DH1 vdd! gnd! sinv
xinvH1 DH1 DH vdd! gnd! sinv

xRegH DH QH CLKH X1 gnd! snandinv

xinvQH QH notQH vdd! gnd! sinv
cH1 notQH 0 5f
xInvQH2 QH notQH2 vdd! gnd! sinv
cH2 notQH2 gnd! 5f
xInvQH3 QH notQH3 vdd! gnd! sinv
cH3 notQH3 gnd! 5f
xinvQH4 QH notQH4 vdd! gnd! sinv
cH4 notQH4 gnd! 5f
*************************************

*****DEFINE STIMULI***********
Vdd vvdd 0 "v_supply"
Vvdd vdd! 0 "v_supply"
Vgnd gnd! 0 0.0V
Vdsetup vdd! X0 DC=0V
Vdhold vdd! X1 DC=0V

VclockS CLKS0 gnd! pulse 0 "v_supply" 1n 5p 5p 1n 2n
VclockH CLKH0 gnd! pulse 0 "v_supply" 1n 5p 5p 1n 2n 
Vds DS0 gnd! PWL 0n 0.0V "3.005n-delay" 0.0V "3.025n-delay" "v_supply" 4n "v_supply" $ rise de 20p
Vdh DH0 gnd! PWL 0n 0.0V 2.35n 0.0V 2.4n "v_supply" "3.005n-delay" "v_supply" "3.025n-delay" 0.0V 4n 0.0V
*****************************************************

***********DEFINE ANALYSIS**********
.tran STEP=1p STOP=4.6n START=2.5n
**********************************

***********ALTER STATEMENTS********
.alter case 2: transicao de 1 para 0
Vds DS0 gnd! PWL 0n "v_supply" "3.005n-delay" "v_supply" "3.025n-delay" 0.0V 4n 0.0V
Vdh DH0 gnd! PWL 0n "v_supply" 2.35n "v_supply" 2.4n 0.0V "3.005n-delay" 0.0V "3.025n-delay" "v_supply" 4n "v_supply"

.end
